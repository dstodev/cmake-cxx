cmake_minimum_required(VERSION 3.18)

message(DEBUG "CMAKE_BUILD_TYPE: ${CMAKE_BUILD_TYPE}")

project(SampleProject)

cmake_policy(SET CMP0091 NEW)  # https://cmake.org/cmake/help/latest/policy/CMP0091.html

#[[
	The following snippet looks for project-config.cmake anywhere in the primary
	build directory.

	This sample project builds in primary-build-dir/sample, but it is not
	part of the primary project. It is built separately, but looks for
	the package generated by the primary project.

	project-config.cmake is generated by the primary project as part of its
	distribution package. It mostly contains target exports.

	- ${PROJECT_BINARY_DIR} = primary-build-dir/sample
	- ${PROJECT_BINARY_DIR}/../ = primary-build-dir

	When CPack generates a ZIP package, it generates a real ZIP file, but it
	also generates an unzipped directory which contains the same files.

	Thus, search the primary build directory for project.cmake, which will
	reside in this unzipped package directory.
]]
set(config_file "${PROJECT_BINARY_DIR}/../project-config.cmake")

file(GLOB_RECURSE project_exports "${config_file}")
list(FILTER project_exports INCLUDE REGEX "TXZ")
list(LENGTH project_exports project_exports_length)

if(NOT project_exports_length EQUAL 1)
	message(FATAL_ERROR "Cannot locate ${config_file}")
endif()

get_filename_component(exports_dir "${project_exports}" DIRECTORY)
set(package_root "${exports_dir}/..")

list(APPEND CMAKE_MODULE_PATH "${package_root}/cmake")
include(project-config)

set(CMAKE_MSVCIDE_RUN_PATH "${package_root}/bin")
set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>DLL" CACHE STRING "MSVC runtime")

set(target main)

add_executable(${target})

include(sources.cmake)

target_link_libraries(main
	PRIVATE
		project::project
		project::cxxopts
)

add_custom_target(run
	COMMAND ${target} > ${target}.txt
	COMMAND ${CMAKE_COMMAND} -E cat ${target}.txt
	VERBATIM
)
